Se desea modelar en Haskell los árboles con información en las hojas (y sólo en ellas). 

Para esto introduciremos el siguiente tipo:

```haskell
data AIH a = Hoja a | Bin (AIH a) (AIH a)
```
$\textcolor{aqua}{a)}$ Definir el esquema de recursión estructural `foldAIH` y dar su tipo. 

Por tratarse del primer esquema de recursión que tenemos para este tipo, se permite usar recursión explícita.

```hs
foldAIH :: (a -> b) -> (b -> b -> b) -> AIH a -> b
foldAIH cHoja cBin (Hoja x) = cHoja x
foldAIH cHoja cBin (Bin i d) = cBin (rec i) (rec d)
    where rec = foldAIH cHoja cBin
```

---
$\textcolor{aqua}{b)}$ Escribir las funciones `altura
:: AIH a -> Integer` y `tamaño :: AIH a -> Integer`.

Considerar que la altura de una hoja es $1$ y el tamaño de un `AIH` es su cantidad de hojas.

```hs
altura :: AIH a -> Integer
altura (Hoja _) = 1
altura (Bin i d) = 1 + max (altura i) (altura d)
```

```hs
altura :: AIH a -> Integer
altura = foldAIH (const 1) (\rec_i rec_d -> 1 + max rec_i rec_d)
```

```hs
tamaño :: AIH a -> Integer
tamaño (Hoja _) = 1
tamaño (Bin i d) = tamaño i + tamaño d
```

```hs
tamaño :: AIH a -> Integer
tamaño = foldAIH (const 1) (+)
```
