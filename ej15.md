$\textcolor{aqua}{i.}$ Definir el tipo `RoseTree` de árboles no vacíos, con una cantidad indeterminada de hijos para cada nodo.

```haskell
data RoseTree a = Rose a [RoseTree a]
```

---
$\textcolor{aqua}{ii.}$ Escribir el esquema de recursión estructural para `RoseTree`. Importante escribir primero su tipo.

```haskell
foldRoseTree :: (a -> [b] -> b) -> RoseTree a -> b
foldRoseTree cRose (Rose x xs) = cRose x (map rec xs)
  where rec = foldRoseTree cRose
```

---
$\textcolor{aqua}{iii.}$ Usando el esquema definido, escribir las siguientes funciones:

$\textcolor{aqua}{a)}$ `hojas`, que dado un `RoseTree`, devuelva una lista con sus hojas ordenadas de izquierda a derecha, según su aparición en el `RoseTree`.

```haskell
hojas :: RoseTree a -> [a]
hojas (Rose a []) = [a]
hojas (Rose _ xs) = concatMap hojas xs
```
`concatMap` le aplica hojas a cada `RoseTree` en la lista `xs`, consigue una lista de hojas para cada `RoseTree` y luego concatena todas esas listas teniendo así todas las hojas del RoseTree original

```haskell
hojas :: RoseTree a -> [a]
hojas = foldRoseTree (\x rec -> if null rec then [x] else concat rec)
```
En este caso usamos concat porque rec ya es una lista de listas de hojas.

---
$\textcolor{aqua}{b)}$ `distancias`, que dado un `RoseTree`, devuelva las distancias de su raíz a cada una de sus hojas.

```haskell
distancias :: RoseTree a -> [Int]
distancias (Rose _ []) = [0]
distancias (Rose _ xs) = map (+1) (concatMap distancias xs)
```

```haskell
distancias :: RoseTree a -> [Int]
distancias = foldRoseTree (\_ rec -> if null rec then [0] else map (+1) (concat rec))
```

---
$\textcolor{aqua}{c)}$ `altura`, que devuelve la altura de un `RoseTree` (la cantidad de nodos de la rama más larga). 

Si el `RoseTree` es una hoja, se considera que su altura es $1$.

```haskell
altura :: RoseTree a -> Int
altura (Rose _ []) = 1
altura (Rose _ xs) = 1 + maximum (map altura xs)
```

```haskell
altura :: RoseTree a -> Int
altura = foldRoseTree (\_ hijos -> if null hijos then 1 else 1 + maximum hijos)
```